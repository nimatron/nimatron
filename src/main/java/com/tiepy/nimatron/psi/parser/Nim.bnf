/*
 * Copyright 2020 TiePy Ltd. All rights reserved.
 * DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
 *
 * This file is part of Nimatron, a Nim plug-in for IDEA platform IDEs.
 *
 * Nimatron is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 2 of the License, or
 * (at your option) any later version.  TiePy Ltd designates this
 * particular file as subject to the "Classpath" exception as provided
 * by TiePy Ltd in the LICENSE file that accompanied this code.
 *
 * Nimatron is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Nimatron.  If not, see <https://www.gnu.org/licenses/>.
 *
 * Please contact TiePy Ltd, 19 Lyall Way, Laurencekirk, AB30 1FU, UK
 * or visit www.tiepy.com if you need additional information or have any
 * questions.
 */

{
  parserClass="com.tiepy.nimatron.psi.parser.NimParser"

  implements="com.tiepy.nimatron.psi.NimElement"
  extends="com.tiepy.nimatron.psi.impl.NimElementImpl"

  psiClassPrefix="Nim"
  psiImplClassSuffix="Impl"
  psiPackage="com.tiepy.nimatron.psi"
  psiImplPackage="com.tiepy.nimatron.psi.impl"

  elementTypeHolderClass="com.tiepy.nimatron.psi.NimElementTypes"
  elementTypeClass="com.tiepy.nimatron.psi.NimElementType"
  tokenTypeClass="com.tiepy.nimatron.psi.NimTokenType"

  implements("procStmt|funcStmt|methodStmt|iteratorStmt|macroStmt|templateStmt|converterStmt")="com.tiepy.nimatron.psi.NimNamedElement"
  mixin(     "procStmt|funcStmt|methodStmt|iteratorStmt|macroStmt|templateStmt|converterStmt")="com.tiepy.nimatron.psi.impl.NimNamedElementImpl"

  tokens = [
    BRACKET="BRACKET"
    C_SEMICOLON="C_SEMICOLON"
    C_COMMA="C_COMMA"
    C_GRAVE_ACCENT="C_GRAVE_ACCENT"
  ]
}

//module = stmt ^* (';' / IND{=})
module ::= IND_EQ* stmts? semicolon? IND_EQ*
stmts ::= stmt ((semicolon | IND_EQ) stmt)*

//comma = ',' COMMENT?
comma ::= ',' COMMENT?

//semicolon = ';' COMMENT?
semicolon ::= ';' COMMENT?

//colon = ':' COMMENT?
colon ::= ':' COMMENT?

//colcom = ':' COMMENT?
colcom ::= ':' COMMENT?

op0 ::= OP0A|OP0B|OP0C
op1 ::= OP1A|OP1B|OP1C|OP1D
op2 ::= OP2A|OP2B|OP2C
op3 ::= OP3A|OP3B
op4 ::= OP4A
op5 ::= OP5A|OP5B|OP5C|OP5D|OP5E|OP5F|OP5G|OP5H|OP5I|OP5J|OP5K|OP5L|OP5M
op6 ::= OP6A
op7 ::= OP7A
op8 ::= OP8A|OP8B
op9 ::= OP9A|OP9B|OP9C|OP9D|OP9E|OP9F|OP9G
op10 ::= OP10A|OP10B

//operator =  OP0 | OP1 | OP2 | OP3 | OP4 | OP5 | OP6 | OP7 | OP8 | OP9
//         | 'or' | 'xor' | 'and'
//         | 'is' | 'isnot' | 'in' | 'notin' | 'of' | 'as' | 'from' |
//         | 'div' | 'mod' | 'shl' | 'shr' | 'not' | 'static' | '..'
operator ::= op0|op1|op2|op3|op4|op5|op6|op7|op8|op9|'from'|'static'|'..'

//prefixOperator = operator

//optInd = COMMENT? IND?
termInd ::= DEDENT | <<eof>>
meta optInd ::= <<RULE>> | (INDENT <<RULE>>? COMMENT? (IND_EQ <<RULE>>? COMMENT?)* termInd)

//optPar = (IND{>} | IND{=})?

//simpleExpr = arrowExpr (OP0 optInd arrowExpr)* pragma?
//arrowExpr = assignExpr (OP1 optInd assignExpr)*
//assignExpr = orExpr (OP2 optInd orExpr)*
//orExpr = andExpr (OP3 optInd andExpr)*
//andExpr = cmpExpr (OP4 optInd cmpExpr)*
//cmpExpr = sliceExpr (OP5 optInd sliceExpr)*
//sliceExpr = ampExpr (OP6 optInd ampExpr)*
//ampExpr = plusExpr (OP7 optInd plusExpr)*
//plusExpr = mulExpr (OP8 optInd mulExpr)*
//mulExpr = dollarExpr (OP9 optInd dollarExpr)*
//dollarExpr = primary (OP10 optInd primary)*
op ::= op0|op1|op2|op3|op4|op5|op6|op7|op8|op9|op10
simpleExpr ::= primary (op <<optInd primary>>)* pragma?

//symbol = '`' (KEYW|IDENT|literal|(operator|'('|')'|'['|']'|'{'|'}'|'=')+)+ '`'|IDENT|KEYW
symbol ::= '`' (KEYW|IDENT|literal|operator|'('|')'|'['|']'|'{'|'}'|'=')+ '`'|IDENT|KEYW

//exprColonEqExpr = expr (':'|'=' expr)?
exprColonEqExpr ::= expr (':'|'=' expr)?

//exprList = expr ^+ comma
exprs ::= expr (comma expr)*

//exprColonEqExprList = exprColonEqExpr (comma exprColonEqExpr)* comma?
exprColonEqExprs ::= exprColonEqExpr (comma exprColonEqExpr)* comma?

//dotExpr = expr '.' optInd (symbol | '[:' exprList ']')

//explicitGenericInstantiation = '[:' exprList ']' ( '(' exprColonEqExpr ')' )?

//qualifiedIdent = symbol ('.' optInd symbol)?
qualifiedIdent ::= symbol ('.' <<optInd symbol>>)?

//setOrTableConstr = '{' ((exprColonEqExpr comma)* | ':' ) '}'
setOrTableConstr ::= '{' ((exprColonEqExpr comma)+ | ':' ) '}' // NOTE: Changed '*' to '+'

//castExpr = 'cast' '[' optInd typeDesc optPar ']' '(' optInd expr optPar ')'
castExpr ::= 'cast' '[' <<optInd typeDesc>> <<optInd (']')>> '(' <<optInd expr>> <<optInd (')')>>

//parKeyw = 'discard' | 'include' | 'if' | 'while' | 'case' | 'try'
//        | 'finally' | 'except' | 'for' | 'block' | 'const' | 'let'
//        | 'when' | 'var' | 'mixin'

//par = '(' optInd
//          ( &parKeyw complexOrSimpleStmt ^+ ';'
//          | ';' complexOrSimpleStmt ^+ ';'
//          | pragmaStmt
//          | simpleExpr ( ('=' expr (';' complexOrSimpleStmt ^+ ';' )? )
//                       | (':' expr (',' exprColonEqExpr     ^+ ',' )? ) ) )
//          optPar ')'
par ::= '(' <<optInd par1>> <<optInd (')')>>
par1 ::= par2
       | par3
       | pragmaStmt
       | par4
par2 ::= stmt (';' stmt)*
par3 ::= ';' stmt (';' stmt)*
par4 ::= simpleExpr (par5 | par6)
par5 ::= '=' expr (';' stmt (';' stmt)*)?
par6 ::= ':' expr (',' exprColonEqExpr (',' exprColonEqExpr)*)?

//literal = | INT_LIT | INT8_LIT | INT16_LIT | INT32_LIT | INT64_LIT
//          | UINT_LIT | UINT8_LIT | UINT16_LIT | UINT32_LIT | UINT64_LIT
//          | FLOAT_LIT | FLOAT32_LIT | FLOAT64_LIT
//          | STR_LIT | RSTR_LIT | TRIPLESTR_LIT
//          | CHAR_LIT
//          | NIL
literal ::= INT_LIT | INT8_LIT | INT16_LIT | INT32_LIT | INT64_LIT
          | UINT_LIT | UINT8_LIT | UINT16_LIT | UINT32_LIT | UINT64_LIT
          | FLOAT_LIT | FLOAT32_LIT | FLOAT64_LIT
          | STR_LIT | RSTR_LIT | TRIPLESTR_LIT
          | CHAR_LIT
          | BOOL_LIT // NOTE: BOOL_LIT was added here.
          | NIL

//generalizedLit = GENERALIZED_STR_LIT | GENERALIZED_TRIPLESTR_LIT
generalizedLit ::= GENERALIZED_STR_LIT | GENERALIZED_TRIPLESTR_LIT

//identOrLiteral = generalizedLit | symbol | literal
//               | par | arrayConstr | setOrTableConstr
//               | castExpr
identOrLiteral ::= generalizedLit | symbol | literal
                 | par | arrayConstr | setOrTableConstr
                 | castExpr

//tupleConstr = '(' optInd (exprColonEqExpr comma?)* optPar ')'

//arrayConstr = '[' optInd (exprColonEqExpr comma?)* optPar ']'
arrayConstr ::= '[' (<<optInd (exprColonEqExpr comma?)>>)* <<optInd (']')>>

//primarySuffix = '(' (exprColonEqExpr comma?)* ')'
//      | '.' optInd symbol generalizedLit?
//      | '[' optInd exprColonEqExprList optPar ']'
//      | '{' optInd exprColonEqExprList optPar '}'
//      | &( '`'|IDENT|literal|'cast'|'addr'|'type') expr # command syntax
primarySuffix ::= primarySuffix1
                | primarySuffix2
                | primarySuffix3
                | primarySuffix4
                | primarySuffix5
primarySuffix1 ::= '(' (exprColonEqExpr comma?)* ')'
primarySuffix2 ::= '.' <<optInd (symbol generalizedLit?)>>
primarySuffix3 ::= '[' <<optInd exprColonEqExprs>> <<optInd (']')>>
primarySuffix4 ::= '{' <<optInd exprColonEqExprs>> <<optInd ('}')>>
primarySuffix5 ::= <<optInd exprs>> // command syntax

//condExpr = expr colcom expr optInd
//        ('elif' expr colcom expr optInd)*
//         'else' colcom expr
condExpr ::= expr colcom <<optInd expr>>
          ('elif' expr colcom <<optInd expr>>)*
           'else' colcom expr

//ifExpr = 'if' condExpr
ifExpr ::= 'if' condExpr

//whenExpr = 'when' condExpr
whenExpr ::= 'when' condExpr

//pragma = '{.' optInd (exprColonEqExpr comma?)* optPar ('.}' | '}')
pragma ::= '{.' <<optInd ((exprColonEqExpr comma?)*)>> <<optInd ('.}'|'}')>>

//identVis = symbol OPR?  # postfix position

// NOTE: The following replaced OPR so that other OPX tokens are also matched.
// NOTE: Intention was to include 'op2' however OP2B ':' seems to cause an unhelpful ambiguity.
//oprCombo ::= OPR|op0|op1|op2|OP5A|OP5B|OP5C|OP5D|OP5E|OP5F|op6|op7|op8|OP9A|OP9B|OP9G|op10
oprCombo ::= OPR|op0|op1|OP2A|OP2C|OP5A|OP5B|OP5C|OP5D|OP5E|OP5F|op6|op7|op8|OP9A|OP9B|OP9G|op10

//identVisDot = symbol '.' optInd symbol OPR?
identVisDot ::= symbol '.' <<optInd symbol>> oprCombo?

//identWithPragma = identVis pragma?
identWithPragma ::= symbol oprCombo? pragma?

//identWithPragmaDot = identVisDot pragma?
identWithPragmaDot ::= identVisDot pragma?

//declColonEquals = identWithPragma (comma identWithPragma)* comma?
//                  (':' optInd typeDesc)? ('=' optInd expr)?
declColonEquals ::= identWithPragma (',' identWithPragma)* ','?
                    (':' <<optInd typeDesc>>)? ('=' <<optInd expr>>)?

//identColonEquals = IDENT (comma IDENT)* comma?
//     (':' optInd typeDesc)? ('=' optInd expr)?)
identColonEquals ::= IDENT (comma IDENT)* comma?
     (':' <<optInd typeDesc>>)? ('=' <<optInd expr>>)?

//inlTupleDecl = 'tuple'
//    '[' optInd  (identColonEquals (comma/semicolon)?)*  optPar ']'

//extTupleDecl = 'tuple'
//    COMMENT? (IND{>} identColonEquals (IND{=} identColonEquals)*)?

//tupleClass = 'tuple'

//paramList = '(' declColonEquals ^* (comma/semicolon) ')'
params ::= '(' (declColonEquals ((comma|semicolon) declColonEquals)*)? ')'

//paramListArrow = paramList? ('->' optInd typeDesc)?
paramsArrow ::= params? ('->' <<optInd typeDesc>>)?

//paramListColon = paramList? (':' optInd typeDesc)?
paramsColon ::= params? (':' <<optInd typeDesc>>)?

//doBlock = 'do' paramListArrow pragma? colcom stmt
doBlock ::= 'do' paramsArrow pragma? colcom <<optInd stmts>>

//procExpr = 'proc' paramListColon pragma? ('=' COMMENT? stmt)?

//distinct = 'distinct' optInd typeDesc

//forStmt = 'for' (identWithPragma ^+ comma) 'in' expr colcom stmt
forStmt ::= 'for' (identWithPragma (comma identWithPragma)*) 'in' expr colcom <<optInd stmts>>

//forExpr = forStmt

//expr = (blockExpr
//      | ifExpr
//      | whenExpr
//      | caseStmt
//      | forExpr
//      | tryExpr)
//      / simpleExpr
expr ::= blockStmt
       | ifExpr
       | whenExpr
       | caseStmt
       | forStmt
       | tryExpr
       | simpleExpr

//typeKeyw = 'var' | 'out' | 'ref' | 'ptr' | 'shared' | 'tuple'
//         | 'proc' | 'iterator' | 'distinct' | 'object' | 'enum'
typeKeyw ::= 'var' | 'out' | 'ref' | 'ptr' | 'shared' | 'tuple'
           | 'proc' | 'iterator' | 'distinct' | 'object' | 'enum'

//primary = typeKeyw optInd typeDesc
//        /  prefixOperator* identOrLiteral primarySuffix*
//        / 'bind' primary
primary ::= primary1
          | primary2
          | primary3
primary1 ::= typeKeyw <<optInd typeDesc>>
primary2 ::= operator* identOrLiteral primarySuffix*
primary3 ::= 'bind' primary

//typeDesc = simpleExpr ('not' expr)?
typeDesc ::= simpleExpr ('not' expr)?

//typeDefAux = simpleExpr ('not' expr)?
//           | 'concept' typeClass
typeDefAux ::= simpleExpr ('not' expr)?
             | 'concept' typeClass

//postExprBlocks = ':' stmt? ( IND{=} doBlock
//                           | IND{=} 'of' exprList ':' stmt
//                           | IND{=} 'elif' expr ':' stmt
//                           | IND{=} 'except' exprList ':' stmt
//                           | IND{=} 'else' ':' stmt )*
postExprBlocks ::= ':' <<optInd stmts>>? ( postExprBlocks1
                                         | postExprBlocks2
                                         | postExprBlocks3
                                         | postExprBlocks4
                                         | postExprBlocks5 )*
postExprBlocks1 ::= IND_EQ doBlock
postExprBlocks2 ::= IND_EQ 'of' exprs ':' <<optInd stmts>>
postExprBlocks3 ::= IND_EQ 'elif' expr ':' <<optInd stmts>>
postExprBlocks4 ::= IND_EQ 'except' exprs ':' <<optInd stmts>>
postExprBlocks5 ::= IND_EQ 'else' ':' <<optInd stmts>>

//exprStmt = simpleExpr
//         (( '=' optInd expr colonBody? )
//         / ( expr ^+ comma
//             postExprBlocks
//           ))?
exprStmt ::= simpleExpr (exprStmt1 | exprStmt2)?
exprStmt1 ::= '=' <<optInd expr>> colonBody?
exprStmt2 ::= expr (comma expr)* postExprBlocks

//importStmt = 'import' optInd expr
//              ((comma expr)*
//              / 'except' optInd (expr ^+ comma))
importStmt ::= 'import' <<optInd expr>> ((comma expr)+ | importStmt1)
importStmt1 ::= 'except' <<optInd (expr (comma expr)*)>>

//exportStmt = 'export' optInd expr
//              ((comma expr)*
//              / 'except' optInd (expr ^+ comma))
exportStmt ::= 'export' <<optInd expr>> ((',' expr)+ | exportStmt1)
exportStmt1 ::= 'except' <<optInd (expr (comma expr)*)>>

//includeStmt = 'include' optInd expr ^+ comma
includeStmt ::= 'include' <<optInd expr>> (comma expr)*

//fromStmt = 'from' expr 'import' optInd expr (comma expr)*
fromStmt ::= 'from' expr 'import' <<optInd expr>> (comma expr)*

//returnStmt = 'return' optInd expr?
returnStmt ::= 'return' <<optInd expr>>?

//raiseStmt = 'raise' optInd expr?
raiseStmt ::= 'raise' <<optInd expr>>?

//yieldStmt = 'yield' optInd expr?
yieldStmt ::= 'yield' <<optInd expr>>?

//discardStmt = 'discard' optInd expr?
discardStmt ::= 'discard' <<optInd expr>>?

//breakStmt = 'break' optInd expr?
breakStmt ::= 'break' <<optInd expr>>?

//continueStmt = 'break' optInd expr?
continueStmt ::= 'break' <<optInd expr>>?

//condStmt = expr colcom stmt COMMENT?
//           (IND{=} 'elif' expr colcom stmt)*
//           (IND{=} 'else' colcom stmt)?
condStmt ::= expr colcom <<optInd stmts>> COMMENT?
             (IND_EQ 'elif' expr colcom <<optInd stmts>>)*
             (IND_EQ 'else' colcom <<optInd stmts>>)?

//ifStmt = 'if' condStmt
ifStmt ::= 'if' condStmt

//whenStmt = 'when' condStmt
whenStmt ::= 'when' condStmt

//whileStmt = 'while' expr colcom stmt
whileStmt ::= 'while' expr colcom <<optInd stmts>>

//ofBranch = 'of' exprList colcom stmt
ofBranch ::= 'of' exprs colcom <<optInd stmts>>

//ofBranches = ofBranch (IND{=} ofBranch)*
//                      (IND{=} 'elif' expr colcom stmt)*
//                      (IND{=} 'else' colcom stmt)?
ofBranches ::= ofBranch (IND_EQ ofBranch)*
                        (IND_EQ 'elif' expr colcom <<optInd stmts>>)*
                        (IND_EQ 'else' colcom <<optInd stmts>>)?

//caseStmt = 'case' expr ':'? COMMENT?
//            (IND{>} ofBranches DED
//            | IND{=} ofBranches)
caseStmt ::= 'case' expr ':'? COMMENT? (caseStmt1 | caseStmt2)
caseStmt1 ::= INDENT ofBranches DEDENT
caseStmt2 ::= IND_EQ ofBranches

//tryStmt = 'try' colcom stmt &(IND{=}? 'except'|'finally')
//           (IND{=}? 'except' exprList colcom stmt)*
//           (IND{=}? 'finally' colcom stmt)?
tryStmt ::= 'try' colcom <<optInd stmts>>
             (IND_EQ? 'except' exprs colcom <<optInd stmts>>)*
             (IND_EQ? 'finally' colcom <<optInd stmts>>)?

//tryExpr = 'try' colcom stmt &(optInd 'except'|'finally')
//           (optInd 'except' exprList colcom stmt)*
//           (optInd 'finally' colcom stmt)?
tryExpr ::= 'try' colcom <<optInd stmts>>
             (<<optInd ('except' exprs colcom <<optInd stmts>>)>>)*
             (<<optInd ('finally' colcom <<optInd stmts>>)>>)?

//exceptBlock = 'except' colcom stmt

//blockStmt = 'block' symbol? colcom stmt
//blockExpr = 'block' symbol? colcom stmt
blockStmt ::= 'block' symbol? colcom <<optInd stmts>>

//staticStmt = 'static' colcom stmt
staticStmt ::= 'static' colcom <<optInd stmts>>

//deferStmt = 'defer' colcom stmt
deferStmt ::= 'defer' colcom <<optInd stmts>>

//asmStmt = 'asm' pragma? (STR_LIT|RSTR_LIT|TRIPLESTR_LIT)
asmStmt ::= 'asm' pragma? (STR_LIT|RSTR_LIT|TRIPLESTR_LIT)

//genericParam = symbol (comma symbol)* (colon expr)? ('=' optInd expr)?
genericParam ::= symbol (comma symbol)* (colon expr)? ('=' <<optInd expr>>)?

//genericParamList = '[' optInd
//  genericParam ^* (comma/semicolon) optPar ']'
genericParams ::= '[' <<optInd
  ((genericParam ((comma|semicolon) genericParam)*)?)>> <<optInd (']')>>

//pattern = '{' stmt '}'
pattern ::= '{' <<optInd stmts>> '}'

//indAndComment = (IND{>} COMMENT)? | COMMENT?

//routine = optInd identVis pattern? genericParamList?
//  paramListColon pragma? ('=' COMMENT? stmt)? indAndComment
routine ::= pattern? genericParams? paramsColon pragma? ('=' COMMENT? <<optInd stmts>>)?

//commentStmt = COMMENT

//section(RULE) = COMMENT? RULE / (IND{>} (RULE / COMMENT)^+IND{=} DED)
//meta section ::= <<RULE>> | (INDENT <<RULE>>? (IND_EQ <<RULE>>?)* termInd)

//objectWhen = 'when' expr colcom objectPart COMMENT?
//            ('elif' expr colcom objectPart COMMENT?)*
//            ('else' colcom objectPart COMMENT?)?

//objectBranch = 'of' exprList colcom objectPart

//objectBranches = objectBranch (IND{=} objectBranch)*
//                      (IND{=} 'elif' expr colcom objectPart)*
//                      (IND{=} 'else' colcom objectPart)?

//objectCase = 'case' identWithPragma ':' typeDesc ':'? COMMENT?
//            (IND{>} objectBranches DED
//            | IND{=} objectBranches)

//objectPart = IND{>} objectPart^+IND{=} DED
//           / objectWhen / objectCase / 'nil' / 'discard' / declColonEquals

//object = 'object' pragma? ('of' typeDesc)? COMMENT? objectPart

//typeClassParam = ('var' | 'out')? symbol
typeClassParam ::= ('var'|'out')? symbol

//typeClass = typeClassParam ^* ',' (pragma)? ('of' typeDesc ^* ',')? &IND{>} stmt
typeClass ::= (typeClassParam (',' typeClassParam)*)? pragma? ('of' (typeDesc (',' typeDesc)*)?)?

//typeDef = identWithPragmaDot genericParamList? '=' optInd typeDefAux
//            indAndComment? / identVisDot genericParamList? pragma '=' optInd typeDefAux
//            indAndComment?
typeDef ::= identWithPragmaDot genericParams? '=' <<optInd typeDefAux>>
            identVisDot genericParams? pragma '=' <<optInd typeDefAux>>

//varTuple = '(' optInd identWithPragma ^+ comma optPar ')' '=' optInd expr
varTuple ::= '(' <<optInd (identWithPragma (comma identWithPragma)*)>> <<optInd (')')>> '=' <<optInd expr>>

//colonBody = colcom stmt postExprBlocks?
colonBody ::= colcom <<optInd stmts>> postExprBlocks?

//variable = (varTuple / identColonEquals) colonBody? indAndComment
variable ::= (varTuple|identColonEquals) colonBody?

//constant = (varTuple / identWithPragma) (colon typeDesc)? '=' optInd expr indAndComment
constant ::= (varTuple|identWithPragma) (colon typeDesc)? '=' <<optInd expr>>

//bindStmt = 'bind' optInd qualifiedIdent ^+ comma
bindStmt ::= 'bind' <<optInd (qualifiedIdent (comma qualifiedIdent)*)>>

//mixinStmt = 'mixin' optInd qualifiedIdent ^+ comma
mixinStmt ::= 'mixin' <<optInd (qualifiedIdent (comma qualifiedIdent)*)>>

//pragmaStmt = pragma (':' COMMENT? stmt)?
pragmaStmt ::= pragma (':' COMMENT? <<optInd stmts>>)?

//simpleStmt = ((returnStmt | raiseStmt | yieldStmt | discardStmt | breakStmt
//           | continueStmt | pragmaStmt | importStmt | exportStmt | fromStmt
//           | includeStmt | commentStmt) / exprStmt) COMMENT?

//complexOrSimpleStmt = (ifStmt | whenStmt | whileStmt
//                    | tryStmt | forStmt
//                    | blockStmt | staticStmt | deferStmt | asmStmt
//                    | 'proc' routine
//                    | 'func' routine
//                    | 'method' routine
//                    | 'iterator' routine
//                    | 'macro' routine
//                    | 'template' routine
//                    | 'converter' routine
//                    | 'type' section(typeDef)
//                    | 'const' section(constant)
//                    | ('let' | 'var' | 'using') section(variable)
//                    | bindStmt | mixinStmt)
//                    / simpleStmt

procStmt ::= 'proc' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
funcStmt ::= 'func' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
methodStmt ::= 'method' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
iteratorStmt ::= 'iterator' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
macroStmt ::= 'macro' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
templateStmt ::= 'template' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
converterStmt ::= 'converter' symbol oprCombo? <<optInd routine>> {methods=[nameIdentifier="symbol"]}
typeStmt ::= 'type' <<optInd typeDef>>
constStmt ::= 'const' <<optInd constant>>
varStmt ::= ('let'|'var'|'using') <<optInd variable>>

//stmt = (IND{>} complexOrSimpleStmt^+(IND{=} / ';') DED) / simpleStmt ^+ ';'

stmt ::= ifStmt COMMENT?
       | whenStmt COMMENT?
       | whileStmt COMMENT?
       | tryStmt COMMENT?
       | forStmt COMMENT?
       | blockStmt COMMENT?
       | staticStmt COMMENT?
       | deferStmt COMMENT?
       | asmStmt COMMENT?
       | procStmt COMMENT?
       | funcStmt COMMENT?
       | methodStmt COMMENT?
       | iteratorStmt COMMENT?
       | macroStmt COMMENT?
       | templateStmt COMMENT?
       | converterStmt COMMENT?
       | typeStmt COMMENT?
       | constStmt COMMENT?
       | varStmt COMMENT?
       | bindStmt COMMENT?
       | mixinStmt COMMENT?
       | returnStmt COMMENT?
       | raiseStmt COMMENT?
       | yieldStmt COMMENT?
       | discardStmt COMMENT?
       | breakStmt COMMENT?
       | continueStmt COMMENT?
       | pragmaStmt COMMENT?
       | importStmt COMMENT?
       | exportStmt COMMENT?
       | fromStmt COMMENT?
       | includeStmt COMMENT?
       | exprStmt COMMENT?
       | COMMENT
