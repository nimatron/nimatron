plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'org.kordamp.gradle.markdown' version '2.0.0'
}

group 'com.tiepy'
version '1.2.3-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version '2020.2'
}

ext.tmpMarkdownInputDir = "${buildDir}/tmp/markdown-inputs".toString()
ext.htmlOutputDir = "${buildDir}/gen-html".toString()

task prepareMarkdownInputs(type: Copy) {
    from("${projectDir}") {
        include 'releasenotes.md'
    }
    into tmpMarkdownInputDir
}

markdownToHtml {
    dependsOn prepareMarkdownInputs
    sourceDir = new File(tmpMarkdownInputDir)
    outputDir = new File(htmlOutputDir)
    inputEncoding = 'UTF-8'
    outputEncoding = 'UTF-8'
}

def readChangeNotes() {
    def lines = new File(htmlOutputDir, 'releasenotes.html').readLines()

    // Drop the top-level "Release notes" header.
    return lines[1..lines.size-1].join('\n')
}

patchPluginXml {
    dependsOn markdownToHtml
    version version
    changeNotes { readChangeNotes() }
}

sourceSets.main.java.srcDirs 'src/main/gen'